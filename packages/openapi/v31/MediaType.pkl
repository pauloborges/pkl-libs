/// Each Media Type Object provides schema and examples for the media type
/// identified by its key.
///
/// <https://spec.openapis.org/oas/v3.1.0.html#media-type-object>
@ModuleInfo { minPklVersion = "0.26.0" }
module openapi.v31.MediaType
extends "Extensible.pkl"

import ".../core.pkl"
import "Encoding.pkl"
import "Example.pkl"
import "Reference.pkl"
import "Schema.pkl"

/// The schema defining the content of the request, response, or parameter.
schema: Schema

/// Example of the media type. The example object *SHOULD* be in the correct
/// format as specified by the media type. The [example] field is mutually
/// exclusive of the [examples] field.
///
/// Furthermore, if referencing a schema which contains an example, the example
/// value *SHALL* override the example provided by the schema.
example: Any(core.mutuallyExclusive(this, examples))?

/// Examples of the media type. Each example object *SHOULD* match the media
/// type and specified schema if present. The [examples] field is mutually
/// exclusive of the [example] field.
///
/// Furthermore, if referencing a schema which contains an example, the examples
/// value *SHALL* override the example provided by the schema.
examples: Mapping<core.NonEmptyString, *Example | Reference>(
  core.mutuallyExclusive(this, example)
)?

/// A map between a property name and its encoding information. The key, being
/// the property name, *MUST* exist in the schema as a property. The encoding
/// object *SHALL* only apply to [requestBody] objects when the media type is
/// `"multipart"` or `"application/x-www-form-urlencoded"`.
encoding: Mapping<core.NonEmptyString, Encoding>?
