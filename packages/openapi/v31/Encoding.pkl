/// A single encoding definition applied to a single schema property.
///
/// <https://spec.openapis.org/oas/v3.1.0.html#encoding-object>
@ModuleInfo { minPklVersion = "0.26.0" }
module orgs.openapis.v31.Encoding
extends "Extensible.pkl"

import ".../core.pkl"
import "Header.pkl"
import "Parameter.pkl"
import "Reference.pkl"

/// The Content-Type for encoding a specific property. Default value depends on
/// the property type:
///
/// - for `object` - `"application/json"`
/// - for `array` â€“ the default is defined based on the inner type
/// - for all other cases the default is `"application/octet-stream"`.
///
/// The value can be a specific media type (e.g. `"application/json"`), a
/// wildcard media type (e.g. `"image/*"`), or a comma-separated list of the two
/// types.
contentType: core.NonEmptyString?

/// A map allowing additional information to be provided as headers, for example
/// `Content-Disposition`. `Content-Type` is described separately and *SHALL* be
/// ignored in this section. This property *SHALL* be ignored if the request
/// body media type is not a `multipart`.
headers: Mapping<core.NonEmptyString, *Header | Reference>?

/// Describes how a specific property value will be serialized depending on its
/// type. See [Parameter] Object for details on the [style] property. The
/// behavior follows the same values as `query` parameters, including default
/// values.
///
/// This property *SHALL* be ignored if the request body media type is not
/// `application/x-www-form-urlencoded` or `multipart/form-data`. If a value is
/// explicitly defined, then the value of [contentType] (implicit or explicit)
/// *SHALL* be ignored.
style: Parameter.Style?

/// When this is `true`, property values of type `array` or `object` generate
/// separate parameters for each value of the array, or key-value-pair of the
/// map. For other types of properties this property has no effect.
///
/// When [style] is `form`, the default value is `true`. For all other styles,
/// the default value is `false`.
///
/// This property *SHALL* be ignored if the request body media type is not
/// `application/x-www-form-urlencoded` or `multipart/form-data`. If a value is
/// explicitly defined, then the value of [contentType] (implicit or explicit)
/// *SHALL* be ignored.
explode: Boolean = style == "form"

/// Determines whether the parameter value *SHOULD* allow reserved characters,
/// as defined by [RFC3986 Section 2.2](https://datatracker.ietf.org/doc/html/rfc3986#section-2.2)
/// `:/?#[]@!$&'()*+,;=` to be included without percent-encoding.
///
/// This property *SHALL* be ignored if the request body media type is not
/// `application/x-www-form-urlencoded` or `multipart/form-data`. If a value is
/// explicitly defined, then the value of contentType (implicit or explicit)
/// *SHALL* be ignored.
allowReserved: Boolean = false
