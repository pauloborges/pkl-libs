/// A container for the expected responses of an operation. The container maps a
/// HTTP response code to the expected response.
///
/// The documentation is not necessarily expected to cover all possible HTTP
/// response codes because they may not be known in advance. However,
/// documentation is expected to cover a successful operation response and any
/// known errors.
///
/// The [default] *MAY* be used as a default response object for all HTTP codes
/// that are not covered individually by the [Responses] Object.
///
/// The [Responses] Object *MUST* contain at least one response code, and if
/// only one response code is provided it *SHOULD* be the response for a
/// successful operation call.
///
/// <https://spec.openapis.org/oas/v3.1.0.html#responses-object>
@ModuleInfo { minPklVersion = "0.26.0" }
module openapi.v31.Responses
extends "Extensible.pkl"

import "Reference.pkl"
import "Response.pkl"

/// The documentation of responses other than the ones declared for specific
/// HTTP response codes
///
/// Use this field to cover undeclared responses.
`default`: (*Response | Reference)?

/// Any HTTP status code can be used as the property name, but only one property
/// per code, to describe the expected response for that HTTP status code.
///
/// This field *MUST* be enclosed in quotation marks (for example, "200") for
/// compatibility between JSON and YAML.
///
/// To define a range of response codes, this field *MAY* contain the uppercase
/// wildcard character X. For example, `"2XX"` represents all response codes
/// between `[200-299]`. Only the following range definitions are allowed:
/// `"1XX"`, `"2XX"`, `"3XX"`, `"4XX"`, and `"5XX"`. If a response is defined
/// using an explicit code, the explicit code definition takes precedence over
/// the range definition for that code.
responses: Mapping<HttpStatusCode, *Response | Reference>(!isEmpty)

typealias HttpStatusCode = String(matches(Regex(#"^[1-5][X0-9]{2}$"#)))

inlineFields { "responses" }
