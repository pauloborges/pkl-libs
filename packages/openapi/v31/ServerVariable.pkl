/// An object representing a Server Variable for server URL template
/// substitution.
///
/// <https://spec.openapis.org/oas/v3.1.0.html#server-variable-object>
@ModuleInfo { minPklVersion = "0.26.0" }
module openapi.v31.ServerVariable
extends "Extensible.pkl"

import ".../core.pkl"

/// An enumeration of string values to be used if the substitution options are
/// from a limited set.
///
/// The array *MUST NOT* be empty.
enum: Listing<core.NonEmptyString>(!isEmpty)?

/// **REQUIRED**.
///
/// The default value to use for substitution, which *SHALL* be sent if an
/// alternate value is not supplied.
///
/// Note this behavior is different than the [Schema] Object's treatment of
/// default values, because in those cases parameter values are optional. If the
/// [enum] is defined, the value *MUST* exist in the enum's values.
default: core.NonEmptyString(enum.isEmpty || enum.toList().contains(this))

/// An optional description for the server variable.
///
/// CommonMark syntax *MAY* be used for rich text representation.
@SourceCode { language = "Markdown" }
description: core.NonEmptyString?
