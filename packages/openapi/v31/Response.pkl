/// Describes a single response from an API Operation, including design-time,
/// static [links] to operations based on the response.
///
/// <https://spec.openapis.org/oas/v3.1.0.html#response-object>
@ModuleInfo { minPklVersion = "0.26.0" }
module openapi.v31.Response
extends "Extensible.pkl"

import ".../core.pkl"
import "Header.pkl"
import "Link.pkl"
import "MediaType.pkl"
import "Reference.pkl"

/// **REQUIRED**.
///
/// A description of the response.
///
/// CommonMark syntax *MAY* be used for rich text representation.
@SourceCode{ language = "Markdown" }
description: core.NonEmptyString

/// Maps a header name to its definition.
///
/// [RFC7230 Page 22](https://datatracker.ietf.org/doc/html/rfc7230#page-22)
/// states header names are case insensitive. If a response header is defined
/// with the name `"Content-Type"`, it *SHALL* be ignored.
headers: Mapping<core.NonEmptyString, *Header | Reference>(
  !containsKey("Content-Type")
)?

/// A map containing descriptions of potential response payloads.
///
/// The key is a media type or media type range, see [RFC7231 Appendix D](https://datatracker.ietf.org/doc/html/rfc7231#appendix-D),
/// and the [value] describes it.
///
/// For responses that match multiple keys, only the most specific key is
/// applicable. e.g. `"text/plain"` overrides `"text/*"`.
content: Mapping<core.NonEmptyString, MediaType>?

/// A map of operations links that can be followed from the response.
///
/// The key of the map is a short name for the link, following the naming
/// constraints of the names for [Component] Objects.
links: Mapping<core.NonEmptyString, *Link | Reference>?
